default map_pos = [0.5, 0.5]
default map_zoom = 1.0

init python:
    station_info = {
    "1": {
        "title": "Jacksonville Station",
        "description": "Serving roughly 48,000 passengers per year, Jacksonville Station opened on January 4, 1974. In our world, east-bound trains toward New Orleans were suspended in 2005 in the wake of hurricane Katrina. The route has yet to resume service.",
        "pos": [0.8040625, 0.7616]
        },
    "2": {
        "title": "Tallahassee Station",
        "description": "Now closed after service was suspended following hurricane Katrina, the station originally opened in 1858. Though closed, the station remains standing, waiting for service to potentially resume through Tallahassee.",
        "pos": [0.7556, 0.7670]
        },
    "3": {
        "title": "Pensacola Station",
        "description": "Opened on March 21, 1993, Pensacola Station suffered a similar fate to other stations in the region, closing its doors after hurricane Katrina in 2005. In 2021, the station was converted into a veteran memorial museum.",
        "pos": [0.6942, 0.7705]
        },
    "4": {
        "title": "Mobile Station",
        "description": "Opened on April 29, 1984, Mobile Station joins the ranks of those passenger train stations shuttered after hurricane Katrina. In 2021, talks of resuming passenger train service to New Orleans began, with the goal of launching in 2024.",
        "pos": [0.6720, 0.7684]
        },
    "5": {
        "title": "Biloxi Station",
        "description": "Biloxi Station, little more than a covered platform at its height, opened on April 29, 1984. After hurricane Katrina in 2005, service to the station was suspended indefinitely.",
        "pos": [0.6610, 0.7745]
        },
    "6": {
        "title": "New Orleans Union Passenger Terminal",
        "description": "Serving around 100,000 passengers per year, New Orleans' Union Passenger Terminal first opened in 1954. The station was renovated after being damaged by hurricane Katrina in 2005. In the course of recovery efforts following Katrina, NOUPT's bus station was repurposed as a temporary jail.",
        "pos": [0.6379, 0.7958]
        },
    "7": {
        "title": "Lafayette Station",
        "description": "Originally opening in 1911, Lafayette Station serves about 3,800 passengers per year. After burning down and being abandoned in May of 2001, the station was restored in 2007, costing $11 million.",
        "pos": [0.5995, 0.7925]
        },
    "8": {
        "title": "Houston Station",
        "description": "Opening its doors in 1959, Houston Station serves roughly 15,800 passengers per year. The station was built to replace Houston's Grand Central Station, whose building eventually became the site of Houston's main post office.",
        "pos": [0.5293, 0.8122]
        },
    "9": {
        "title": "San Antonio Station",
        "description": "Opened in 1998, San Antonio Station serves about 45,000 passengers per year, making it the second busiest in Texas after Fort Worth's Central Station. Two long distance routes operate to and from San Antonio: one continues west to Los Angeles, and the other north east to Chicago.",
        "pos": [0.4664, 0.8160]
        },
    "10": {
        "title": "Del Rio Station",
        "description": "Opened in 1876, Del Rio Station serves around 2,300 passengers per year. In 1920, a new building was completed, replacing the original wood structure.",
        "pos": [0.4193, 0.8157]
        },
    "11": {
        "title": "Alpine Station",
        "description": "A small, unstaffed station, Alpine Station opened in 1946 and serves about 3,600 passengers per year. Though small, the station was built in the Spanish Mission Revival style, making it a match for its home in west Texas.",
        "pos": [0.3604, 0.7811]
        },
    "12": {
        "title": "El Paso Union Depot",
        "description": "Opened in 1906 and rebuilt in 1982, just a few hundred feet from the Mexican border, El Paso's Union Depot served just under 12,000 passengers per year on average. Since 2018, the city's revived streetcar network has served as a connection to the Depot.",
        "pos": [0.3153, 0.7250]
        },
    "13": {
        "title": "Tucson Station",
        "description": "Opened in 1907, Tucson Station serves roughly 19,000 passengers per year. A proposed expansion of service would connect the city to Phoenix by rail.",
        "pos": [0.2221, 0.6901]
        },
    "14": {
        "title": "Yuma Station",
        "description": "Originally opened in 1926, Yuma Station serves around 4,400 passengers per year. East and west bound trains stop only three times a week each in Yuma.",
        "pos": [0.1620, 0.6639]
        },
    "15": {
        "title": "Palm Springs Station",
        "description": "Palm Springs station opened in June of 1997 and serves about 2,200 passengers per year. The station has had problems in recent years with desert sandstorms, their tracks being buried with sand, forcing temporary closures.",
        "pos": [0.1220, 0.6053]
        },
    "16": {
        "title": "Los Angeles Union Station",
        "description": "Opened on May 3, 1939, Los Angeles Union Station serves just under 930,000 passengers in an average year, making it a transit hub in its own right. Many local and regional transit systems connect to the station, including LA Metro, commuter rail lines, buses, and bike shares.",
        "pos": [0.0887, 0.5854]
        },
    "17": {
        "title": "Santa Barbara Station",
        "description": "Opened in 1902, Santa Barbara Station serves around 213,700 passengers per year. Operating at least five trains in each direction daily, the length of the station's platform causes two roads to be blocked when trains are stopped.",
        "pos": [0.0635, 0.5610]
        },
    "18": {
        "title": "San Luis Obispo Station",
        "description": "San Luis Obispo Station first opened on May 4, 1894 and serves an average of just over 75,000 passengers per year. A new building was constructed next to the original in 1943, and in 1971, the original building was demolished and a parking lot was built in its place.",
        "pos": [0.0556, 0.5228]
        },
    "19": {
        "title": "San Jose Diridon Station",
        "description": "Opened in December 1935, San Jose Diridon Station serves about 111,000 passengers per year. The station is one of only four in California built in the Italian Renaissance Revival style, the same as LA Union Station.",
        "pos": [0.0495, 0.4621]
        },
    "20": {
        "title": "Oakland–Jack London Square Station",
        "description": "Oakland–Jack London Square Station opened on May 22, 1995 and serves around 154,000 passengers per year. The station is the western terminus of one of the US' longest and most iconic passenger rail routes between Chicago, IL and Oakland, CA, passing through the heights of the Rocky Mountains.",
        "pos": [0.0484, 0.4377]
        },
    "21": {
        "title": "Sacramento Valley Station",
        "description": "Opened on February 27, 1926, Sacramento Valley Station serves about 456,000 passengers per year. Quite the busy station, it receives 38 daily trains during the week, and 30 on weekends.",
        "pos": [0.0567, 0.4080]
        },
    "22": {
        "title": "Klamath Falls Station",
        "description": "Opened on May 20, 1909 and serving around 20,600 passengers per year, Klamath Falls Station is the southernmost station in Oregon. Trolley connections to the nearby Crater Lake, the deepest lake in North America, are available at the station.",
        "pos": [0.0740, 0.2763]
        },
    "23": {
        "title": "Eugene-Springfield Station",
        "description": "Opened in 1908, Eugene-Springfield Station serves just under 71,000 passengers a year on average. Eugene is home to the University of Oregon, where Ophi went to school. A few of us Watercress devs live here too!",
        "pos": [0.0634, 0.2149]
        },
    "24": {
        "title": "Portland Union Station",
        "description": "Portland Union Station first opened on February 14, 1896 and serves just over 375,000 passengers per year on average. The station is a major transportation hub for the Portland metro area, with connections to light rail, Portland streetcars, and local buses.",
        "pos": [0.0735, 0.1734]
        }
    }

    map_size = (6400., 4267.)
    x_width_16_9 = round(map_size[1] / 9. * 16.)
    corrected_x_factor = map_size[0] / x_width_16_9
    map_y_offset = 0.05

    def set_map_pos(pos):
        global map_pos
        map_pos = pos

    def get_map_pos(pos):
        global map_size, map_pos, map_zoom, x_width_16_9, corrected_x_factor

        map_factor = config.screen_height / map_size[1]
        # corrected_x = ((pos[0] * map_size[0]) + (x_width_16_9 - map_size[0]) / 2.0) / x_width_16_9
        xpos = ((pos[0] - map_pos[0]) * corrected_x_factor * map_zoom + 0.5)
        ypos = ((pos[1] + map_y_offset - map_pos[1]) * map_zoom + 0.5)
        return (xpos, ypos)

    def get_line(index):
        if index < 9:
            return "map_point_line1"
        elif index < 16:
            return "map_point_line2"
        return "map_point_line3"

transform map_point_show_transform(pos=(0.5, 0.5)):
    anchor (0.5, 0.5) pos get_map_pos(pos) zoom map_zoom * config.screen_height / map_size[1] * 1.0
    pause 0.2
    ease 0.5 zoom map_zoom * config.screen_height / map_size[1] * 2
    ease 0.5 zoom map_zoom * config.screen_height / map_size[1]

transform map_point_transform(start_pos=(0.5, 0.5), end_pos=(0.5, 0.5), duration=2.0, delay=0.5):
    anchor (0.5, 0.5) pos get_map_pos(start_pos) zoom map_zoom * config.screen_height / map_size[1]
    pause delay
    ease duration pos get_map_pos(end_pos)

transform map_transform(start_pos=(0.5, 0.5), end_pos=(0.5, 0.5), duration=2.0, delay=0.5):
    anchor (0.5, 0.5) zoom (map_zoom * config.screen_height / map_size[1])  pos (0.5 - (start_pos[0]-0.5) * corrected_x_factor * map_zoom, 0.5 - (start_pos[1]-map_y_offset-0.5) * map_zoom)
    pause delay
    ease duration pos (0.5 - (end_pos[0]-0.5) * corrected_x_factor * map_zoom, 0.5 - (end_pos[1]-map_y_offset-0.5) * map_zoom)

transform map_text_transform(align=(0.5, 0.5), duration=0.4, delay=0.5):
    align align alpha 0 yzoom 0.5
    pause delay
    parallel:
        linear duration alpha 1
    parallel:
        easein duration yzoom 1

transform map_text_autohide_transform(align=(0.5, 0.5), duration=0.4, delay=0.5, hideafter=1.0):
    align align alpha 0 yzoom 0.5
    parallel:
        pause hideafter
        linear duration alpha 0
    parallel:
        pause delay
        parallel:
            linear duration alpha 1
        parallel:
            easein duration yzoom 1

transform map_root_hide_transform:
    linear 0.5 alpha 0

screen map(index=2, animate=False):
    fixed:
        add "map_base"
        if animate:
            $ start_pos = map_pos
            if index > 1:
                $ start_pos = station_info[str(index - 1)]["pos"]
            $ set_map_pos(start_pos)
            $ end_pos = station_info[str(index)]["pos"]
            $ diff_pos = [end_pos[0] - start_pos[0], end_pos[1] - start_pos[1]]
            $ duration = 1.5
            $ start_delay = 1.0
            timer (duration + start_delay) action Show("map", index=index, animate=False)
        else:
            $ start_pos = station_info[str(index)]["pos"]
            $ end_pos = station_info[str(index)]["pos"]
            $ duration = 0.0
            $ start_delay = 0.0
            $ set_map_pos(station_info[str(index)]["pos"])
        
        add "map_blank" at map_transform(start_pos, end_pos, duration, start_delay)
        add "map_route" at map_transform(start_pos, end_pos, duration, start_delay)

        for i in range(1, len(station_info) + 1):
            $ point_pos = station_info[str(i)]["pos"]
            if animate:
                $ point_end_pos = [point_pos[0] - diff_pos[0], point_pos[1] - diff_pos[1]]
            else:
                $ point_end_pos = point_pos
            if i <= index:
                if i == index and not animate:
                    add get_line(i) at map_point_show_transform(point_end_pos)
                elif i == index and animate:
                    add "map_point_unvisited" at map_point_transform(point_pos, point_end_pos, duration, start_delay)
                else:
                    add get_line(i) at map_point_transform(point_pos, point_end_pos, duration, start_delay)
            else:
                add "map_point_unvisited" at map_point_transform(point_pos, point_end_pos, duration, start_delay)
            
        if not animate:
            $ hide_duration = 0.4
            label station_info[str(index)]["title"] at map_text_transform((0.5, 0.7 + map_y_offset), duration=hide_duration, delay=0.0):
                text_size 60
                text_bold True
                text_color gui.accent_color
                text_outlines [ (5,  "#fff", 0, 0) ]

            frame at map_text_transform((0.5, 0.5), duration=hide_duration, delay=0.5):
                background im.FactorScale("cgs/map_description_frame.png", width=2.7, height=1.2)
                xysize (0.7, 0.5)
                offset (-50, -150)
            label station_info[str(index)]["description"] at map_text_transform((0.5, 0.21), duration=hide_duration, delay=0.5):
                xysize (0.7, 0.5)
                text_size 50
                text_yalign 0.5
                text_bold True
                text_color gui.accent_color
                # text_outlines [ (5, gui.selected_color, 0, 0) ]
        elif animate and index > 1:
            label station_info[str(index - 1)]["title"] at map_text_autohide_transform((0.5, 0.7 + map_y_offset), duration=0.3, delay=0.0):
                text_size 60
                text_bold True
                text_color gui.accent_color
                text_outlines [ (5, "#fff", 0, 0) ]
